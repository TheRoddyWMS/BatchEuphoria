/*
 * Copyright (c) 2017 German Cancer Research Center (DKFZ).
 *
 * Distributed under the MIT License (license terms are at https://www.github.com/theroddywms/Roddy/LICENSE.txt).
 */

import org.cyclonedx.gradle.CycloneDxTask

plugins {
    id 'org.ajoberstar.grgit' version '4.1.1'
    id 'org.cyclonedx.bom' version '1.8.2'
    id 'maven-publish'
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'application'

group = "com.github.theroddywms"

mainClassName = 'de.dkfz.roddy.BEIntegrationTestStarter'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

/*
 * Gets the version name from the current Git tag. If the current commit is not tagged,
 * this returned string will indicate that. Also if the repository is dirty.
 */
def getVersionName() {
    def dirtySuffix = grgit.status().isClean() ? '' : '-dirty'
    return grgit.describe() + dirtySuffix
}

rootProject.version = getVersionName()

compileJava.options.compilerArgs = ["-release", targetCompatibility]

// Set -Pchecked on the command line to change this compiler parameter.
if (project.hasProperty("checked") && !project.checked) {
    compileJava.options.compilerArgs += ["-Xlint:unchecked", "-Xlint:deprecation"]
    compileGroovy.options.compilerArgs += ["-Xlint:unchecked", "-Xlint:deprecation"]
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.7'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.7'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.7.1'
    implementation 'org.codehaus.groovy:groovy-all:2.4.21'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.spockframework:spock-core:1.1-groovy-2.4'
    testRuntimeOnly "net.bytebuddy:byte-buddy:1.6.5"
    testRuntimeOnly "org.objenesis:objenesis:2.5.1"
    testImplementation 'org.slf4j:slf4j-nop:1.7.25'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.5.0'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.11.0'
    implementation 'com.google.guava:guava:32.0.0-android'
    implementation 'com.github.theroddywms:RoddyToolLib:2.4.1'

    // Runtime retention of @NotNull and annotations.
    implementation 'com.github.bbottema:jetbrains-runtime-annotations:1.0.1'
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/pom.xml")
    }
}

publishing {
    publications {
        mavenJava(org.gradle.api.publish.maven.MavenPublication) {
            from components.java
            pom {
                name = 'BatchEuphoria'
                url = "https://www.github.com/theroddywms/BatchEuphoria"
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/eilslabs/BatchEuphoria/master/LICENSE'
                    }
                }
            }
        }
    }
}

jar {
    version = getVersionName()

    manifest {
        attributes("Implementation-Vendor": "German Cancer Research Center (DKFZ)")
        attributes("Implementation-Title": archiveBaseName.get())
        attributes 'Implementation-Version': rootProject.version
    }

    into("META-INF/maven/$project.group/$project.name") {
        from generatePomFileForMavenJavaPublication
//        from "$buildDir/pom.xml"
    }

}

task listJars {
    doLast {
        configurations.compile.each { File file -> println file.name }
    }
}


task uberjar(type: Jar, dependsOn:[':compileJava',':compileGroovy']) {
    from files(sourceSets.main.output.dirs)
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
//    .asFileTree.files.collect { zipTree(it) }

    version = getVersionName()

    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Implementation-Version': version
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

task listConfigurations {
    group = "reporting"
    description = "List all configurations, as needed for discerning development and production configurations by the SBOM tasks."
    doLast {
        configurations.each { Configuration c ->
            println c.name
        }
    }
}

task allBoms() {
    // a grouping task to execute the CycloneDxTask for each configuration
    group = "reporting"
}

rootProject.configurations.each { configuration ->
    String name = configuration.name
    task "${name}Bom"(type: CycloneDxTask) {
        group = "reporting"
        description = "Creates a CycloneDX SBOM for the configuration ${name}."

        destination = file("${project.buildDir}/reports/cyclonedx")
        outputName = name
        includeConfigs = [name]
        schemaVersion = "1.5"
        outputFormat="json"
        includeLicenseText = false
        projectType = "application"
    }
    allBoms.dependsOn tasks.getByName("${name}Bom")
}
